wrapper.gradleVersion = '5.0'

// Major.Minor build version - this should be considered the release version number, and should be incremented manually when required
def buildVersion = '1.0'

def currentVersion = "${buildVersion}.${rootProject.buildRevision}"

println "Build version: $currentVersion"
println "Gradle wrapper version: ${wrapper.gradleVersion}"

allprojects {
    group = 'roramu.service'
    version = currentVersion
}

subprojects {
    apply plugin: 'java-library'
    apply plugin: 'maven-publish'

    // Get external repositories from a separate file (to allow for CI automation to provide different values)
    apply from: rootProject.file('repositories.gradle').absolutePath

    dependencies {
        testCompile 'org.junit.jupiter:junit-jupiter-api:5.3.2'
        testCompile 'org.junit.jupiter:junit-jupiter-params:5.3.2'
        testCompile 'org.junit.jupiter:junit-jupiter-engine:5.3.2'
    }

    task sourcesJar(type: Jar) {
        from sourceSets.main.allJava
        classifier = 'sources'
    }

    task javadocJar(type: Jar) {
        from javadoc
        classifier = 'javadoc'
    }

    // Get external publishing behavior from a separate file (to allow for CI automation to provide different values)
    apply from: rootProject.file('publishing.gradle').absolutePath
}
